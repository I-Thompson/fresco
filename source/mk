#!/bin/tcsh
#***********************************************************************
#     
#    Copyright 2017, I.J. Thompson
#     
#    This file is part of FRESCO.
#
#    FRESCO is free software: you can redistribute it and/or modify it
#    under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 3 of the License, or
#    (at your option) any later version.
#     
#    FRESCO is distributed in the hope that it will be useful, but
#    WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
#    GNU General Public License for more details.
#     
#    You should have received a copy of the GNU General Public License
#    along with FRESCO. If not, see <http://www.gnu.org/licenses/>.
#     
#    OUR NOTICE AND TERMS AND CONDITIONS OF THE GNU GENERAL PUBLIC
#    LICENSE
#
#    The precise terms and conditions for copying, distribution and
#    modification are contained in the file COPYING.
#
#***********************************************************************

#
# Usage   
# 	mk 			 : first compiler for your ARCH
# 	mk <compiler> 		 : use this compiler
# 	mk <compiler> <options>  

set COMP = 'f90'
set PREF='a'
set FCOPT = ''
# change to any local preferences: 
#  copy & edit these lines yourself

if(! $?ARCH && -X arch) setenv ARCH `arch`
if( ${ARCH} == 'arm64' ) then
      if( -X gfortran ) 	set COMP = 'gfortran'
      if( -X nagfor ) 	        set COMP = 'nagfor'
      set PREF=${ARCH}
endif
if( ${ARCH} == 'x86_64' ) then   
      if( -X pgf90 )   set COMP = 'pgf90'
      if( -X gfortran ) 	set COMP = 'gfortran'
      if( -X pathf95 ) set COMP = 'pathf95'
      if( -X f90 ) 	set COMP = 'f90'
      set PREF=${ARCH}
endif
if( ${ARCH} == 'i686' ) then 	# Linux Intel
      if( -X pgf90 )   set COMP = 'pgf90'
      if( -X pathf95 ) set COMP = 'pathf95'
      if( -X ifort )   set COMP = 'ifort'
      set PREF=${ARCH}
endif
if( ${ARCH} == 'ia64' ) then    # Linux Itanium
      if( -X ifort )   set COMP = 'ifort'
      set PREF=${ARCH}
endif
if( ${ARCH} == 'ppc' ) then 	# Mac Powerpc
      if( -X xlf )     set COMP = 'xlf'
      set PREF=${ARCH}
endif
if( ${ARCH} == 'i386' ) then 	# Mac Intel
      if( -X gfortran )     set COMP = 'gfortran'
      if( -X ifort )   set COMP = 'ifort'
      if( -X f95 )     set COMP = 'f95'
      if( -X pgf90 )   set COMP = 'pgf90'
      set PREF=${ARCH}
endif
if( ${ARCH} == 'sun4' || ${ARCH} == 'sun4u' ) then 	# Sun sparc
      if( -X f90 )     set COMP = 'f90'
      set PREF=${ARCH}
endif
if( ${ARCH} == 'i86pc' ) then 	# Sun opterons
      if( -X f90 )     set COMP = 'f90'
      set PREF=${ARCH}
endif
if( ${ARCH} == 'pwr5' ) then    # uP
      if( -X xlf90 )     set COMP = 'xlf90'
      set PREF=${ARCH}
endif
echo From arch=${ARCH}, expect compiler ${COMP}

 if ($#argv != 0)  then
 set try=$1
 if (-X ${try} && ${try} != 'fresco' && ${try} != 'sfresco' && ${try} != 'install' ) then
	set COMP = $try
	shift
 endif
 endif
set DIR = ${PREF}-${COMP}

if( ! -e ${DIR}) mkdir ${DIR}
echo Choose compiler ${COMP} in directory ${DIR}
chdir ${DIR}
echo ln -s ../*.f . >& /dev/null
ln -s ../*.f . >& /dev/null
echo \cp -f ../makefile . 
\cp -f ../makefile . 

set FCOPT = ''
if ( ${COMP} == 'ifort')     set FCOPT = '-fast'   # link step is slow!
if ( ${COMP} == 'ifort')     set FCOPT = '-O3'
if ( ${COMP} == 'gfortran')  set FCOPT = '-O3 -std=legacy'

echo make ${argv}  FC=${COMP} FFLAGS="${FCOPT}"

make ${argv} FC=${COMP} FFLAGS="${FCOPT}"
